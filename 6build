#!/usr/bin/env perl

# Copyright (C) 2015 by cygx <cygx@cpan.org>
# Distributed under the Boost Software License, Version 1.0

use strict;
use warnings;

use open ':encoding(utf8)';

my $USAGE = <<'EOU';
Usage:

  6build [<OPT>] status

  6build [<OPT>] config [<BUILD>]
         [--<CONFIG>=<NAME>...] [--<FLAG>...] [<VAR>=<VALUE>...]

  6build [<OPT>] make [<TARGET>...]

  6build [<OPT>] {reconfig|tryconfig} [<BUILD>]
         [--<CONFIG>=<NAME>...] [--<FLAG>...] [<VAR>[~|+|-]=<VALUE>...]
         [--no-<CONFIG|FLAG|VAR>...]

  6build [<OPT>] {autoconf|automake [<TARGET>...]}
         [--<CONFIG>=<NAME>...] [--<FLAG>...] [<VAR>=<VALUE>...]

  <OPT> = -C[ ]<DIR>       run in directory <DIR>
          -v               verbose output
          -q               quiet output
          -{v|q}C[ ]<DIR>  combined options
EOU

my $AUTOFILE = '6.auto';
my $CONFFILE = '6.config';
my $STATFILE = '6.status';
my $MAKEFILE = '6.make';

my $verbose = 0;
my $quiet = 0;

if (@ARGV && $ARGV[0] =~ /^-([vq]?)(?:(C)(.*))?$/) {
    shift @ARGV;
    if ($1 eq 'v') { $verbose = 1 }
    elsif ($1 eq 'q') { $quiet = 1 }
    if (defined $2) {
        my $dir = $3 || shift(@ARGV) || die "No dir passed to cd into";
        chdir($dir) or die "Cannot cd into '$dir'";
    }
}

({  status    => sub { die 'TODO' },
    config    => sub { die 'TODO' },
    reconfig  => sub { die 'TODO' },
    tryconfig => sub { die 'TODO' },
    make      => \&make,
    autoconf  => \&autoconf,
    automake  => \&automake,
    '-h'      => \&help,
    '--help'  => \&help,
}->{@ARGV ? $ARGV[0] : '-h'} // \&unknown)->(@ARGV);

my ($build, %configs, %flags, %vars);
my (@rules, %named_rules, %rules_by_out_files, %rules_by_in_files);

sub say { print @_, "\n" unless $quiet }
sub cry { warn @_, "\n" unless $quiet }
sub println { print @_, "\n" }
sub note { print @_, "\n" if $verbose }

sub glob6_parse_string_to {
    my $end = shift;
    my @toks;

    for (;;) {
        if (!length) { die "missing terminator '$end'" }
        elsif (s/^\\(.[^\\\/\?\*\(\)\[\]\{\}]*)//) { push @toks, $1 }
        elsif (s/^([^\\\/\?\*\(\)\[\]\{\}]+)//) { push @toks, $1 }
        elsif (s/^\Q$end\E//) { last }
        else {
            my $c = substr($_, 0, 1);
            die "unescaped special char '$c'";
        }
    }

    join '', @toks;
}

sub glob6_parse {
    local $_ = shift;
    my (@ops, @part);

    for (;;) {
        if (/^$/) {
            push @ops, 'FILE', scalar(@part), @part;
            last;
        }
        elsif (/^\/$/) {
            push @ops, 'DIR', scalar(@part), @part;
            last;
        }
        elsif (/^\(\/\)$/) {
            push @ops, 'FILE|DIR', scalar(@part), @part;
            last;
        }
        elsif (s/^\///) {
            push @ops, 'SUBDIR', scalar(@part), @part;
            @part = ();
        }
        elsif (s/^\?//) {
            push @part, 'QUERY';
        }
        elsif (s/^\*\*//) {
            die "subtree wildcard '**' invalid within filename" if @part;
            push @ops, 'SUBTREE';
            @part = ('STAR');
        }
        elsif (s/^\*//) {
            push @part, 'STAR';
        }
        elsif (s/^\(//) {
            push @part, 'OPT', glob6_parse_string_to(')');
        }
        elsif (s/^\[//) {
            my $chars = glob6_parse_string_to(']');
            die "empty character class '[]'" unless length $chars;
            push @part, 'CLASS', $chars;
        }
        elsif (/^\{/) {
            my @alt;
            push @alt, glob6_parse_string_to('}') while s/^\{//;
            push @part, 'ALT', scalar(@alt), @alt;
        }
        else {
            my @toks;
            while (length) {
                if    (s/^\\(.[^\\\/\?\*\(\)\[\]\{\}]*)//) { push @toks, $1 }
                elsif (s/^([^\\\/\?\*\(\)\[\]\{\}]+)//) { push @toks, $1 }
                else { last }
            }
            push @part, 'STR', join('', @toks);
        }
    }

    \@ops;
}

sub glob6_rx {
    my @rx;

    while (@_) {
        my $tok = shift;
        if    ($tok eq 'STR' )  { push @rx, quotemeta(shift) }
        elsif ($tok eq 'QUERY') { push @rx, '.' }
        elsif ($tok eq 'STAR' ) { push @rx, '.*' }
        elsif ($tok eq 'OPT')   { push @rx, '(?:', quotemeta(shift), ')?' }
        elsif ($tok eq 'CLASS') { push @rx, '[', quotemeta(shift), ']' }
        elsif ($tok eq 'ALT') {
            my $n = shift;
            push @rx, '(?:', join('|', map(quotemeta, splice(@_, 0, $n))), ')';
        }
        else { die "unknown token '$tok'" }
    }

    my $rx = join '', @rx;
    qr/^$rx$/;
}

sub glob6_tree {
    my ($tree, $dir) = @_;
    push @$tree, $dir;

    opendir my $dh, $dir or die "$dir: $!";
    my @nodes = readdir $dh;
    closedir $dh;

    for (@nodes) {
        my $path = "$dir/$_";
        glob6_tree($tree, $path) if !/^\./ && -d $path;
    }
}

sub glob6_exec {
    my $res = shift;
    my $dir = shift;
    local $_;

    my $op = shift;
    if ($op eq 'FILE' || $op eq 'DIR' || $op eq 'FILE|DIR') {
        my $n = shift;
        my $rx = glob6_rx splice(@_, 0, $n);

        opendir my $dh, $dir or die "$dir: $!";
        while (readdir $dh) {
            if (/$rx/) {
                my $path = "$dir/$_";
                push @$res, $path
                    if ($op eq 'FILE' && -f $path) ||
                       ($op eq 'DIR'  && -d $path) ||
                       (-f $path || -d $path);
            }
        }
        closedir $dh;
    }
    elsif ($op eq 'SUBDIR') {
        my $n = shift;
        my $rx = glob6_rx splice(@_, 0, $n);

        opendir my $dh, $dir or die "$dir: $!";
        my @nodes = readdir $dh;
        closedir $dh;

        for (@nodes) {
            if (/$rx/) {
                my $path = "$dir/$_";
                glob6_exec($res, $path, @_) if -d $path;
            }
        }
    }
    elsif ($op eq 'SUBTREE') {
        my @subdirs;
        glob6_tree(\@subdirs, $dir);
        glob6_exec($res, $_, @_) for @subdirs;
    }
    else { die "unknown operation '$op'" }
}

sub glob6 {
    my @res;

    for (@_) {
        my $ops = glob6_parse($_);
        glob6_exec(\@res, '.', @$ops);
    }

    @res;
}

sub words {
    local $_ = @_ ? $_[0] : $_;
    my @esc = /\\(.)/g;
    s/\\./<!>/g;
    my @words = split /\s+/;
    for (@words) {
        while (/<!>/) {
            my $esc = shift @esc;
            s/<!>/$esc/;
        }
    }
    @words;
}

sub parse_args {
    # TODO: parse --no-
    for (@_) {
        if (/^\w+$/) {
            cry "Replacing build '$build' with '$_'"
                if defined $build;
            $build = $_;
            next;
        }

        my ($flag, $name, $value) = /^(--)?(\w[\w-]*)(?:=(.*))?$/;
        die "Failed to parse '$_'"
            unless defined($name) && (defined($flag) || defined($value));

        if (!defined($flag)) {
            cry "Replacing var '$name' with '$value'"
                if exists $vars{$name};
            $vars{$name} = $value;
        }
        elsif (!defined($value)) {
            $flags{$name} = 1;
        }
        else {
            cry "Replacing config '--$name' with '$value'"
                if exists $configs{$name};
            $configs{$name} = $value;
        }
    }
}

sub autoconf {
    shift;
    parse_args(@_);

    open my $fh, '<', $AUTOFILE or die "$AUTOFILE: $!";
    while (<$fh>) {
        chomp;
        next if /^\s*$/;

        say $_;

        my (@flags, @configs, @dynconfigs, @vars, @dynvars);
        my @toks = words;
        for (@toks) {
            my ($cond, $flag, $name, $value) =
                /^([?!])?(--)?(\w[\w-]*)(?:=(.*))?$/;

            die "Failed to parse '$_'"
                unless defined($name) && (defined($flag) || defined($value));

            my $store = defined($flag) ?
                (defined($value) ? \%configs : \%flags) : \%vars;

            my $exists = exists $store->{$name};
            my $prevalue = $store->{$name} if $exists;

            if (!defined $cond) {
                goto SKIP if $exists;
                if (!defined($flag)) {
                    if ($value eq '*') { push @dynvars, $name }
                    else { push @vars, [ $name, $value ] }
                }
                elsif (!defined($value)) {
                    push @flags, $name;
                }
                else {
                    if ($value eq '*') { push @dynconfigs, $name }
                    else { push @configs, [ $name, $value ] }
                }
            }
            elsif ($cond eq '?' && $exists) {
                goto SKIP unless !defined($value) || $value eq $prevalue;
            }
            elsif ($cond eq '!' && $exists) {
                goto SKIP unless !defined($value) || $value ne $prevalue;
            }
            else { die sprintf "Precondition '%s$name' not met", $flag // '' }
        }

        while (<$fh>) {
            chomp;
            last if /^\s*$/;

            my @dollars = /\$(\$|\*?\w+)|\$\((\*?\w+)\)/g;
            s/\$(\$|\*?\w+|\(\*?\w+\))/<!>/g;
            while (/<!>/) {
                shift(@dollars) =~ /^(\*)?(.+)/;
                my $dollar = defined($1) ? $ENV{$2} : $vars{$2};
                s/<!>/$dollar/;
            }

            say $_;

            my @out = map { s/\r\n$/\n/r } qx/$_ 2>&1/;
            my $code = $? >> 8;

            if ($verbose) {
                print "| $_" for @out
            }

            if ($code != 0) {
                say '[ ] failed';
                next;
            }

            $configs{$_->[0]} = $_->[1]
                for @configs;

            $flags{$_} = 1
                for @flags;

            if (@dynconfigs) {
                my (%in, %out);
                @in{@dynconfigs} = ();

                for (@out) {
                    if (/^--(\w+)=(\N*)/) {
                        if (exists $in{$1}) {
                            $out{$1} = $2;
                            delete $in{$1};
                        }
                    }
                }

                if (%in) {
                    say '[?] incomplete';
                    next;
                }

                @configs{keys %out} = values %out;
            }

            if (@dynvars) {
                my (%in, %out);
                @in{@dynvars} = ();

                for (@out) {
                    if (/^(\w+)=(\N*)/) {
                        if (exists $in{$1}) {
                            $out{$1} = $2;
                            delete $in{$1};
                        }
                    }
                }

                if (%in) {
                    say '[?] incomplete';
                    next;
                }

                @vars{keys %out} = values %out;
            }

            if (@vars) {
                die 'TODO';
            }

            say '[x] check', "\n";
            goto DONE;
        }

        say;
        next;

    SKIP:
        say '[-] skipped', "\n";

    DONE:
        while (<$fh>) {
            chomp;
            last if /^\s*$/;
        }
    }
    close $fh;

    say 'Configuration:';

    println $_ for map {
        my ($k, $v) = ($_, $configs{$_});
        "--$k=$v";
    } sort keys %configs;

    println $_ for map {
        my ($k, $v) = ($_, $flags{$_});
        next unless $v;
        "--$_";
    } sort keys %flags;

    println $_ for map {
        my ($k, $v) = ($_, $vars{$_});
        "$k=$v";
    } sort keys %vars;
}

sub visit {
    my ($seq, $visited, $made, $active, $rule) = @_;
    my @in = @{$rule->{in}};
    my @out = @{$rule->{out}};

    for (@out) {
        die "Cyclical build of file '$_' detected"
            if exists $active->{$_};
    }

    return if exists $visited->{$rule->{id}};

    for (@out) {
        die "Repeated build of file '$_' detected"
            if exists $made->{$_};

        $active->{$_} = 1;
    }

    for (@in) {
        next unless exists $rules_by_out_files{$_};
        my @candidates = @{$rules_by_out_files{$_}};
        die "No candidates for building file '$_'"
            if @candidates == 0;

        my $next;
        if (@candidates == 1) { $next = $candidates[0] }
        else {
            my $file = $_;
            for (@candidates) {
                my $name = $_->{name};
                if (defined($name) && $file eq $name) {
                    $next = $_;
                    last;
                }
            }

            die "Ambiguous rules for building file '$_'"
                unless defined $next;
        }

        visit($seq, $visited, $made, $active, $next);
    }

    for (@out) {
        $made->{$_} = 1;
        delete $active->{$_};
    }

    $visited->{$rule->{id}} = 1;
    push @$seq, $rule;
}

sub make {
    shift;
    my $id = 0;

    open my $fh, '<', $MAKEFILE or die "$MAKEFILE: $!";
    while (<$fh>) {
        chomp;
        next if /^\s*$/;

        my ($lhs, $rhs) = split />/, $_, 2;
        my ($name, @in) = split /\|/, $lhs;
        my @out = split /\|/, $rhs // '';
        my @body;

        my $rule = {
            id   => $id++,
            name => length($name) ? $name : undef,
            in   => \@in,
            out  => \@out,
            body => \@body,
        };

        if (length($name)) {
            die "Redefinition of rule '$name'"
                if exists $named_rules{$name};

            $named_rules{$name} = $rule;
        }

        push @{$rules_by_out_files{$_}}, $rule for @out;
        push @{$rules_by_in_files{$_}}, $rule for @in;
        push @rules, $rule;

        while (<$fh>) {
            chomp;
            last if /^\s*$/;
            push @body, $_;
        }
    }
    close $fh;

    my @targets;
    if (@_) {
        for (@_) {
            if (exists $named_rules{$_}) {
                push @targets, $named_rules{$_};
            }
            elsif (exists $rules_by_out_files{$_}) {
                die "No unambiguous way to make target '$_'"
                    unless @{$rules_by_out_files{$_}} == 1;

                push @targets, $rules_by_out_files{$_}->[0];
            }
            else { die "Unknown target '$_'" }
        }
    }
    else {
        die 'No rules available' unless @rules;
        push @targets, $rules[0];
    }

    my (@seq, %visited, %made, %active);
    visit(\@seq, \%visited, \%made, \%active, $_)
        for @targets;

    for (@seq) {
        for (@{$_->{body}}) {
            say $_;
            my @out = map { s/\r\n$/\n/r } qx/$_ 2>&1/;
            my $code = $? >> 8;
            if ($verbose) {
                print "| $_" for @out
            }

            die 'TODO' if $code != 0;
        }
    }
}

sub automake {
    shift;
    my (@targets, @options);
    for (@_) {
        if (/^--|=/) { push @options, $_ }
        else { push @targets, $_ }
    }

    autoconf @options;
    make @targets;
}

sub help { print $USAGE }

sub unknown {
    $_ = shift;
    my $type = /^-/ ? 'flag' : 'subcommand';
    die "Unknown $type '$_'";
}

__END__

=pod

=encoding utf8

=head1 NAME

6build - Yet another build system

=head1 SYNOPSIS

    6build status

    6build config win32 --debug
    6build make test

    6build tryconfig --no-debug
    6build make test

    6build reconfig --no-debug
    6build make install

    6build autoconf
    6build make test

    6build automake test install


=head1 COPYRIGHT AND LICENSE

Copyright (C) 2015 by L<cygx|mailto:cygx@cpan.org>

Distributed under the
L<Boost Software License, Version 1.0|http://www.boost.org/LICENSE_1_0.txt>

=cut

Boost Software License - Version 1.0 - August 17th, 2003

Permission is hereby granted, free of charge, to any person or organization
obtaining a copy of the software and accompanying documentation covered by
this license (the "Software") to use, reproduce, display, distribute,
execute, and transmit the Software, and to prepare derivative works of the
Software, and to permit third-parties to whom the Software is furnished to
do so, all subject to the following:

The copyright notices in the Software and this entire statement, including
the above license grant, this restriction and the following disclaimer,
must be included in all copies of the Software, in whole or in part, and
all derivative works of the Software, unless such copies or derivative
works are solely in the form of machine-executable object code generated by
a source language processor.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE, TITLE AND NON-INFRINGEMENT. IN NO EVENT
SHALL THE COPYRIGHT HOLDERS OR ANYONE DISTRIBUTING THE SOFTWARE BE LIABLE
FOR ANY DAMAGES OR OTHER LIABILITY, WHETHER IN CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
DEALINGS IN THE SOFTWARE.
